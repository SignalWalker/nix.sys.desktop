#! /usr/bin/env zsh

set -e

autoload colors; colors

printerr() { printf "${fg[yellow]}[.list-inputs]${reset_color} %s\n" "$@" 1>&2; }

if [[ ! -v HOME ]]; then
	printerr '$HOME not set'
	exit 1
fi

PROJECTS=${XDG_PROJECTS_DIR:-$HOME/projects}

if [[ ! -d "$PROJECTS" ]]; then
	printerr "$PROJECTS is inaccessible || not a directory"
	exit 1
fi

sys=$PROJECTS/nix/sys
hm=$PROJECTS/nix/home
nixmod=$PROJECTS/nix/mod

typeset -A INPUTS
INPUTS=( \
	[homelib]="$hm/lib" \
	[homebase]="$hm/base" \
	[homedev]="$hm/dev" \
	[homedev/ashvim]="$PROJECTS/cfg/neovim" \
	[homedesk]="$hm/desktop" \
	[homedesk/watch-battery]="$PROJECTS/misc/batwatch" \
	[homemedia]="$hm/media" \
	[sysbase]="$sys/base" \
	[sysbase/nginx-vhost-defaults]="$nixmod/nginx-vhost-defaults"
	[syshome]="$sys/home" \
	[sysdesk]="$sys/desktop"
)

for INPUT PATH in ${(kv)INPUTS}; do
	if [[ ! -d "$PATH" ]]; then
		printerr "$PATH is inaccessible || not a directory"
		exit 1
	fi
	if [[ ! -e "$PATH/flake.nix" ]]; then
		printerr "$PATH does not contain flake.nix"
		exit 1
	fi
done


if [[ (! -v 1) || $1 == "overrides" ]]; then
	prefix=""
	if [[ -v 2 ]]; then
		prefix="git+ssh://$2.ashwalker.net"
		printerr "using prefix: $prefix"
	fi
	for INPUT PATH in ${(kv)INPUTS}; do
		printf "--override-input\n%q\n%q\n" "$INPUT" "$prefix$PATH"
	done
elif [[ $1 == "paths" ]]; then
	if [[ -v 2 ]]; then
		printerr "unexpected prefix argument: \`$2\`"
		exit 1
	fi
	print -r -- ${(v)INPUTS}
else
	printerr "expected one of [\"paths\", \"overrides\"], got: \"$1\""
	exit 1
fi


